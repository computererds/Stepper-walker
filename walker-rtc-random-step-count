#include <RTClib.h>

RTC_DS3231 rtc;

#define STEP_PIN 3
#define DIR_PIN  4
#define EN_PIN   5

int movement_steps = 444;  //100 DEGREES ON MY STEPPER
int lastWalkDay = -1;      
bool planGenerated = false; 

int randomStartHour, randomStartMin;
long totalStrides;

void setup() {
  Serial.begin(9600);
  pinMode(STEP_PIN, OUTPUT);
  pinMode(DIR_PIN, OUTPUT);
  pinMode(EN_PIN, OUTPUT);
  digitalWrite(EN_PIN, HIGH); 

  if (!rtc.begin()) {
    while (1); 
  }

  // SET TIME ON UPLOAD
  rtc.adjust(DateTime(F(__DATE__), F(__TIME__)));
  randomSeed(analogRead(A0));
  
  // SET THE FIRST WALK
  generateDailyPlan();
}

void generateDailyPlan() {
  DateTime now = rtc.now();

  // SET RANDOMIZED START TIME AND STRIDE COUNT
  randomStartHour = random(9, 18);  // HOUR BETWEEN 9am & 5pm
  randomStartMin = random(0, 60);  // RANDOMIZE MINUTE
  totalStrides = random(1000, 20001);  // RANDOMIZE STRIDE COUNT
  planGenerated = true;

  Serial.println("\n================================");
  Serial.print("Start time: ");
  Serial.print(now.hour()); Serial.print(":");
  if(now.minute() < 10) Serial.print("0");
  Serial.println(now.minute());
  
  Serial.print("Scheduled for: "); Serial.print(randomStartHour); 
  Serial.print(":"); if(randomStartMin < 10) Serial.print("0");
  Serial.println(randomStartMin);
  Serial.print("Stride count: "); Serial.println(totalStrides);
  Serial.println("================================\n");
}

void performStride() {
  digitalWrite(EN_PIN, LOW);
  digitalWrite(DIR_PIN, HIGH); 
  for(int i = 0; i < movement_steps; i++) { 
    int speed = (i < 50) ? map(i, 0, 50, 800, 400) : 400; // FORWARD ARM SWING SPEED
    digitalWrite(STEP_PIN, HIGH); delayMicroseconds(speed);
    digitalWrite(STEP_PIN, LOW); delayMicroseconds(speed);
  }
  delay(50);  //FRONT TRANSITION DELAY
  digitalWrite(DIR_PIN, LOW); 
  for(int i = 0; i < movement_steps; i++) {
    int speed = (i < 50) ? map(i, 0, 50, 800, 500) : 500;  //BACKWARD ARM SWING SPEED
    digitalWrite(STEP_PIN, HIGH); delayMicroseconds(speed);
    digitalWrite(STEP_PIN, LOW); delayMicroseconds(speed);
  }
  delay(80);  //REAR TRASITION DELAY
}

void loop() {
  DateTime now = rtc.now();

  // CHECK FOR NEW DAY & SCHEDULE WALK
  if (now.day() != lastWalkDay && !planGenerated) {
    generateDailyPlan();
  }

  // WALK TRIGGER
  if (planGenerated && now.day() != lastWalkDay) {
    if (now.hour() == randomStartHour && now.minute() == randomStartMin) {
      Serial.println("Starting scheduled walk");
      
      for (long s = 1; s <= totalStrides; s++) {
        performStride();
        if (s % 1000 == 0) { 
            Serial.print("Progress: "); 
            Serial.print(s); 
            Serial.print(" / "); 
            Serial.println(totalStrides); 
        }
      }
      
      digitalWrite(EN_PIN, HIGH); 
      lastWalkDay = now.day(); // MARK TODAY AS DONE
      planGenerated = false;   // FLAG TOMORROW WALK TO BE GENERATED
      Serial.println("Walk complete");
    }
  }

  delay(5000); 
}
